program contabil_ghinionist;
const nmax=50;
      fin='tabel.in';
      fout='tabel.out';
var a:array[1..50,1..50] of integer;
    n,m,p:Integer;
    k:integer;{k=nr. de gauri}
    dl,dc:array[1..nmax] of byte;{nr. de defecte : dc[i]-pe coloana i, dl[i]-pe linia i}

procedure citire;
var f:text;pp,i,j,x:integer;
begin
 fillchar(a,sizeof(a),255);
 assign(f,fin);reset(f);
 readln(f,n,m);k:=n*m;{tot tabelul e plin de defecte}
 readln(f,p);
 fillchar(dl,sizeof(dl),m);{la inceput avem numai defecte pe linii}
 fillchar(dc,sizeof(dc),n);
 for pp:=1 to p do
  begin
   dec(k);
   readln(f,i,j,x);
   a[i,j]:=x;
   dec(dl[i]);{scade nr. de defecte pe linia i}
   dec(dc[j]);
  end;
 close(f);
end;

procedure scrie_sol;
var f:text;i,j:integer;
begin
 assign(f,fout);rewrite(f);
 if k>0 then writeln(f,'NU')
 else
 for i:=1 to n do
  begin
   for j:=1 to m-1 do  {scriem elem. de pe o linie cu un spatiu dupa, mai putin ultimul elem}
     write(f,a[i,j],' ');
   writeln(f,a[i,m]);{scriem ultimul elem. al liniei si trecem la randul urmator}
  end;
 close(f);
end;

procedure repara_linia(x:integer);
var i,s,p:integer;
begin
p:=m;s:=0;
for i:=1 to m-1 do
  if a[x,i]<0 then p:=i{memoram pozitia defectului}
              else s:=s+a[x,i];{adunam elementele la suma}
if p=m then {daca p=m inseamna ca defectul e pe ultima coloana}
          a[x,m]:=s
       else
         a[x,p]:=a[x,m]-s;
dec(dl[x]);{scade nr. de defecte pe linia si coloana elementului reparat}
dec(dc[p]);
dec(k);
end;

procedure repara_coloana(x:integer);
var i,s,p:integer;
begin
p:=n;s:=0;
for i:=1 to n-1 do
  if a[i,x]<0 then p:=i{memoram pozitia defectului}
              else s:=s+a[i,x];{adunam elementele la suma}
if p=n then {daca p=n inseamna ca defectul e pe ultima linie}
          a[n,x]:=s
       else
         a[p,x]:=a[n,x]-s;
dec(dl[p]);{scade nr. de defecte pe linia si coloana elementului reparat}
dec(dc[x]);
dec(k);{scade nr. total de defecte}
end;

procedure rezolva;
var ok:boolean;
    i:integer;
begin
 ok:=false;
 while (k>0)and not ok do
  begin
   ok:=true;
   for i:=1 to n do if dl[i]=1 then begin ok:=false;repara_linia(i);end;
   for i:=1 to m do if dc[i]=1 then begin ok:=false;repara_coloana(i);end;
  end;
end;

BEGIN
citire;
rezolva;
scrie_sol;
END.